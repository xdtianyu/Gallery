apply plugin: 'com.android.application'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git describe --abbrev=0 --tags'
        .execute([], project.rootDir).text.trim().replaceAll("v", "")
def gitTagCount = Integer.parseInt('git rev-list --tags --no-walk --count'
        .execute([], project.rootDir).text.trim())

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "org.xdty.gallery"

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode gitTagCount
        versionName gitTag

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        setProperty("archivesBaseName", "Gallery-v$versionName")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile('proguard-test-rules.pro')
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.28.2'

    androidTestImplementation "androidx.annotation:annotation:${rootProject.ext.supportVersion}"

    androidTestImplementation 'androidx.test:runner:1.3.0-alpha02'
    // UiAutomator Testing
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'org.hamcrest:hamcrest-integration:1.3'

    androidTestImplementation 'androidx.test:rules:1.3.0-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.3.0-alpha02'
    androidTestImplementation('androidx.test.espresso:espresso-contrib:3.3.0-alpha02') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'recyclerview-v7'
    }

    implementation "androidx.appcompat:appcompat:${rootProject.ext.supportVersion}"
    implementation "com.google.android.material:material:1.1.0-alpha09"
    implementation "org.samba.jcifs:jcifs:1.3.17"
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.3'

    implementation 'com.squareup.okhttp3:okhttp:3.13.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'

    implementation 'com.squareup.retrofit2:retrofit:2.1.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    implementation('com.squareup.retrofit:converter-simplexml:1.9.0') {
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
    }

    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation('com.github.bumptech.glide:okhttp3-integration:1.4.0') {
        exclude group: 'glide-parent'
    }

    implementation 'org.xdty.webdav:webdav:0.1.9'

    //    compile(name:'webdav-debug', ext:'aar')

    implementation 'com.jenzz:materialpreference:1.3'
    implementation 'cat.ereza:customactivityoncrash:1.5.0'

    implementation 'io.requery:requery:1.4.0'
    implementation 'io.requery:requery-android:1.4.0'
    annotationProcessor 'io.requery:requery-processor:1.4.0'

    implementation 'com.google.code.gson:gson:2.8.5'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation "io.reactivex.rxjava2:rxjava:2.2.11"

    implementation 'com.google.dagger:dagger:2.19'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.19'

    debugImplementation 'com.facebook.stetho:stetho:1.4.2'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.4.2'
    debugImplementation 'com.facebook.stetho:stetho-okhttp:1.4.2'
    debugImplementation 'com.facebook.stetho:stetho-urlconnection:1.4.2'

    implementation 'me.drakeet.multitype:multitype:4.0.0-alpha2'

    implementation project(path: ':photoview')
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
}

apply from: '../signing.gradle'
apply from: '../manifest.gradle'
